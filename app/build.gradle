buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        // the latest version of the android-apt plugin
        classpath 'io.fabric.tools:gradle:1.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        debug {
            Properties props = new Properties()
            try {
                props.load(new FileInputStream(".signing/debug.properties"))
                storeFile file('../.signing/debug.jks')
                storePassword(props['DEBUG_STORE_PASSWORD'])
                keyAlias(props['DEBUG_KEY_ALIAS'])
                keyPassword(props['DEBUG_KEY_PASSWORD'])
            } catch (IOException ignore) {
                println "Missing debug keystore and/or properties"
                throw ignore
            }
        }
        release {
            Properties props = new Properties()
            try {
                props.load(new FileInputStream(".signing/release.properties"))
                storeFile file('../.signing/release.jks')
                storePassword(props['RELEASE_STORE_PASSWORD'])
                keyAlias(props['RELEASE_KEY_ALIAS'])
                keyPassword(props['RELEASE_KEY_PASSWORD'])
            } catch (IOException e) {
                println "Missing release keystore and/or properties"
                throw e
            }
        }
        upload {
            Properties props = new Properties()
            try {
                props.load(new FileInputStream(".signing/upload-keystore.properties"))
                storeFile file('../.signing/upload-keystore.jks')
                storePassword(props['RELEASE_STORE_PASSWORD'])
                keyAlias(props['RELEASE_KEY_ALIAS'])
                keyPassword(props['RELEASE_KEY_PASSWORD'])
            } catch (IOException e) {
                println "Missing upload keystore and/or properties"
                throw e
            }
        }
    }
    configurations.all {
        resolutionStrategy.force 'com.android.support:support-v13:27.1.1'
    }
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.pixplicity.cryptogram"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 204
        versionName "1.8.11"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "BITCOIN_ADDRESS", "\"35YPTPGZNeVrUNFA9Tn7A8HVx4nhFd1989\"")
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        upload {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.upload
        }
        uploadDebug {
            minifyEnabled false
            debuggable true
            jniDebuggable true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.upload
        }
    }
}

task deleteTestResources(type: Delete) {
    delete "${projectDir}/src/test/resources/assets", "${projectDir}/src/test/resources/res"
}
task copyTestAssets(type: Copy) {
    from "${projectDir}/src/main/assets"
    into "${projectDir}/src/test/resources/assets"
}
task copyTestResources(type: Copy) {
    from "${projectDir}/src/main/res"
    into "${projectDir}/src/test/resources/res"
}

tasks.whenTaskAdded { task ->
    println 'task check: ' + task.getName()
    if (task.name == 'assembleDebug' || task.name == 'assembleRelease') {
        deleteTestResources.execute()
        copyTestAssets.execute()
        copyTestResources.execute()
    }
}

repositories {
    maven { url "https://dl.bintray.com/pixplicity/maven" }
    maven { url "https://dl.bintray.com/drummer-aidan/maven" }
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:support-annotations:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'

    implementation 'com.android.billingclient:billing:1.1'

    implementation 'com.google.android.gms:play-services-games:11.4.2'

    implementation 'com.google.firebase:firebase-core:11.4.2'
    implementation 'com.google.firebase:firebase-perf:11.4.2'

    implementation 'net.soulwolf.widget:ratiolayout:1.0.0'
    implementation 'com.afollestad.material-dialogs:core:0.9.4.7'
    implementation 'com.afollestad:easyvideoplayer:0.3.0'

    implementation 'com.github.bumptech.glide:glide:4.6.1'

    implementation 'com.squareup:otto:1.3.8'

    implementation 'com.pixplicity.htmlcompat:library:1.1.1'
    implementation 'com.pixplicity.easyprefs:library:1.9.0'
    implementation 'com.pixplicity.generate:library:1.2.0'

    implementation 'com.google.code.gson:gson:2.8.2'

    implementation 'com.getkeepsafe.taptargetview:taptargetview:1.10.0'

    implementation('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true
    }

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testImplementation 'junit:junit:4.12'
    // Be careful updating; consult https://github.com/powermock/powermock/wiki/Mockito
    testImplementation 'org.mockito:mockito-core:2.8.0'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.3'
    testImplementation 'org.powermock:powermock-module-junit4-rule-agent:1.7.3'
    testImplementation 'org.powermock:powermock-module-junit4-rule:1.7.3'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.3'
}
